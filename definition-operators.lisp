;;; -*- Mode: lisp; Syntax: ansi-common-lisp; Base: 10; Package: org.apache.thrift.implementation; -*-

(in-package :org.apache.thrift.implementation)

;;; This file defines the thrift IDL operators for the `org.apache.thrift` library.
;;;
;;; copyright 2010 [james anderson](james.anderson@setf.de)
;;;
;;; Licensed to the Apache Software Foundation (ASF) under one
;;; or more contributor license agreements. See the NOTICE file
;;; distributed with this work for additional information
;;; regarding copyright ownership. The ASF licenses this file
;;; to you under the Apache License, Version 2.0 (the
;;; "License"); you may not use this file except in compliance
;;; with the License. You may obtain a copy of the License at
;;; 
;;;   http://www.apache.org/licenses/LICENSE-2.0
;;; 
;;; Unless required by applicable law or agreed to in writing,
;;; software distributed under the License is distributed on an
;;; "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
;;; KIND, either express or implied. See the License for the
;;; specific language governing permissions and limitations
;;; under the License.


;;; The Common Lisp backend for the Thrift IDL translator[[1]] generates Lisp source code in terms of the
;;; following definition operators:
;;;
;;;   def-constant
;;;   def-eum
;;;   def-struct
;;;   def-exception
;;;   def-request-method
;;;   def-response-method
;;;   def-service
;;;
;;; The syntax resembles that of the standard Lisp operators. The primary distinction is that identifiers are
;;; the original strings from the Thrift IDL source. The macro operators canonicalize and intern these
;;; according to the current package and read table case. The original values are retained to use as method
;;; and class names for encoding/decoding.
;;;
;;; The interface  definitions can incorporate structures in variable definitions, and the service definitions
;;; entail method definitions, which in turn require structure definitions in order to compile codecs
;;; in-line. This suggests the following file load order and organization:
;;;
;;;   <service>-types.lisp : (generated) enums, structs, exceptions, services
;;;   <service>-vars.lisp : (generated) constants
;;;   <service.lisp : (authored) the base function definitions
;;;
;;; The extra file for constants is required, as the generator emits them before the structs.
;;;
;;; [1]: $THRIFT/compiler/src/generate/t_cl_generator.cc


;;
;;; primitive constructors

(defun thrift:map (&rest tups)
  "Represent map objects as hash tables.
 NB. in order to effect equality when the keys themselves are maps, this and the transport operations
 would need to maintain a global registry."
  (declare (dynamic-extent tups))
  (let ((tbl (make-hash-table :test 'equal)))
     (loop for (key . value) in tups
           do (setf (gethash key tbl) value))
     tbl))

(defun thrift:list (&rest values)
  values)

(defun thrift:set (&rest values)
  values)

          
(defun parm-to-field-decl (parameter-spec)
  "Convert a specialize parameter declaration into the form for a structure field declaration
     (id-name type id)  -> (id-name default &key type id documentation)
 The format appearance in service method declarations is translated to the field for for use
 in request/response argument structures."

  (destructuring-bind (identifier type id &optional default) parameter-spec
    `(,identifier ,default :id ,id :type ,type)))


;;;
;;; definition operators

(defmacro def-package (name &key use)
  (let ((request-name (cons-symbol :keyword name :-request))
        (response-name (cons-symbol :keyword name :-response)))
    `(macrolet ((ensure-package (name &rest options)
                  `(let ((package (find-package ',name)))
                     (cond (package 
                            ,@(let ((use (assoc :use options)))
                                (when use `((use-package ',(rest use) package))))
                            package)
                           (t
                            (defpackage ,name ,@options))))))
       ;; the 'application' package is linked to cl and thrift with shodows
       (eval-when (:load-toplevel :compile-toplevel :execute)
         (ensure-package ,name
                         (:use :common-lisp :thrift ,@use)
                         (:shadowing-import-from :common-lisp :byte :list :map :set :type-of))
         ;; the request/respone packages are isolated
         (ensure-package ,request-name (:use))
         (ensure-package ,response-name (:use))))))


(defmacro def-enum (identifier entries)
  (let ((name (cons-symbol *package* identifier))
        (value-names (mapcar #'(lambda (entry) (str-sym identifier "." (car entry))) entries)))
    ;; define the type, leave the keys are string
    (let ((values (mapcar #'rest entries)))
      (assert (stringp identifier))
      (assert (every #'integerp values))
      `(progn (setf (get ',name 'thrift::enum-members) ',values
                    (get ',name 'thrift::enum-alist) ',entries)
              ,@(mapcar #'(lambda (entry) `(defconstant ,(str-sym identifier "." (car entry)) ,(rest entry)))
                        entries)
              (eval-when (:load-toplevel :compile-toplevel :execute)
                (export '(,name ,@value-names) (symbol-package ',name)))
              ',name))))


(defmacro def-constant (identifier val)
  "Generate a defparameter form, as the 'constants' are often bound to constructed values."
  (assert (stringp identifier))
  `(defparameter ,(str-sym identifier) ,val))


(defmacro def-struct (identifier fields &rest options)
  "DEF-STRUCT identifier [doc-string] ( field-specifier* ) option*
 [Macro]

 field-specifier ::= ( field-identifier default &key type id documentation )
 option ::= (:documentation docstring)
          | (:metaclass metaclass)
          | (:identifier identifier)

 Define a thrift struct with the declared fields. The class and field names are computed by cononicalizing the
 respective identifier and interning it in the current *package*. Each identifier remains associated with its
 metaobject for codec use. Options allow for an explicit identifier, a metacoal other than thrift-struct-class,
 and a documentation string.

 The class is bound to its name as bout the thrift class and CLOS class."

  (let ((metaclass (or (second (assoc :metaclass options)) 'thrift-struct-class))
        (identifier (or (second (assoc :identifier options)) identifier))
        (condition-class (second (assoc :condition-class options)))
        (name (cons-request-symbol identifier))
        (make-name (cons-request-symbol "make-" identifier))
        (slot-names nil)
        (documentation nil))
    (when (stringp fields)
      (shiftf documentation fields (pop options)))
    (setf slot-names (loop for (identifier) in fields collect (str-sym identifier)))
    ;; make the definitions available to compile codecs
    `(eval-when (:compile-toplevel :load-toplevel :execute)
       (defun ,make-name (&rest -initargs- &key ,@slot-names)
         (declare (ignore ,@slot-names))
         (apply #'make-instance ',name -initargs-))
       (defclass ,name (thrift-object)
         ,(loop for field in fields
                collect (destructuring-bind (slot-identifier default &key type id documentation (optional nil o-s))
                                            field
                          (assert (typep id 'fixnum))
                          `(,(str-sym slot-identifier)
                            :initarg ,(cons-symbol :keyword slot-identifier)
                            :accessor ,(cons-request-symbol identifier "-" slot-identifier)
                            ,@(when type `(:type ,type))
                            :identifier-number ,id
                            :identifier-name ,slot-identifier
                            ,@(when (or default (eq type 'bool)) `(:initform ,default))         ; are any bool fields optional?
                            ,@(when o-s `(:optional ,optional))
                            ,@(when documentation `(:documentation ,(string-trim *whitespace* documentation))))))
         (:metaclass ,metaclass)
         (:identifier ,identifier)
         ,@(when condition-class `((:condition-class ,condition-class)))
         ,@(when documentation `((:documentation ,(string-trim *whitespace* documentation)))))
       ,@(unless (eq metaclass 'thrift-exception-class)
           `((export '(,name ,make-name
                       ,@(loop for (slot-identifier) in fields
                               collect (cons-request-symbol identifier "-" slot-identifier)))
                     (symbol-package ',name))
             (setf (find-thrift-class ',name) (find-class ',name)))))))


(defmacro def-exception (identifier fields &rest options)
  "DEF-EXCEPTION identifier [doc-string] ( field-specifier* ) option*
 [Macro]

 field-specifier ::= ( field-identifier default &key type id documentation )
 option ::= (:documentation docstring)
          | (:metaclass metaclass)
          | (:identifier identifier)

 Define a thrift exception with the declared fields. This involves two classes. A condition is defined
 to use as a signal/error argument and a proxy struct class is defined for codec use.
 The proxy class is bound as the class name's thrift class, while the struct class is bound as the
 CLOS class."
  
  (let* ((metaclass (or (second (assoc :metaclass options)) 'thrift-exception-class))
         (identifier (or (second (assoc :identifier options)) identifier))
         (name (str-sym identifier))
         (struct-identifier (concatenate 'string identifier "ExceptionClass"))
         (struct-name (str-sym struct-identifier))
         (documentation nil))
    (when (stringp fields)
      (shiftf documentation fields (pop options)))
    ;; the definitions are used to compile codecs
    `(eval-when (:compile-toplevel :load-toplevel :execute)
       (export ',name (symbol-package ',name))
       (def-struct ,struct-identifier
         ,fields
         (:identifier ,identifier)
         (:metaclass ,metaclass)
         (:condition-class ,name)
         ,@options)
       (define-condition ,name (application-error)
         ,(loop for field in fields
                collect (destructuring-bind (slot-identifier default &key type id documentation optional)
                                            field
                          (declare (ignore id optional))
                          `(,(str-sym slot-identifier)
                            :initarg ,(cons-symbol :keyword slot-identifier)
                            :accessor ,(str-sym identifier "-" slot-identifier)
                            ,@(when type `(:type ,type))
                            ,@(when default `(:initform ,default))
                            ,@(when documentation `(:documentation ,(string-trim *whitespace* documentation))))))
         ,@(when documentation `((:documentation ,(string-trim *whitespace* documentation))))
         ,@(remove-if-not #'(lambda (key) (member key '(:default-initargs :documentation :report)))
                          options :key #'first))
       (defmethod thrift-error-format-control ((error ,name))
         (concatenate 'string (call-next-method)
                      ,(format nil "岷簋磲疸狎＇骈蝮骈屐潴┅┅ㄤ彐礤翳镤翳蜷骠弪蝻颦骘蝽狒狎珲礤铘è弪蝻钺礤┅ㄡ痧孱ㄣ犰飙铄舡礤翳镤扉篝括祜镳骘箪雉殇孱糸骈弪轭骈屐潴泔祆邈啜篝颦簌殇孱糸骈弪箪雉殇孱糸骈弪弪蝻颟┅┅箦翩ㄦ轭洵翳蜷骠沆狍К钺礤ㄦ轭洵沆狍К篝蝓泗钺礤┅┅ㄤ彐躅珏铄蜥翦篝蝓泗溴泔溴痱雉沆狍蟓骘蝽骈屐洵溴骈铋糸镱屮趄岘骈屐洵痨轶舂⑶孱弪狒骘蝽麒殂溴泔溴翳玳鲥篝蝓泗骈屐轭扉铄幸显鲠蜷徕戾怙躅麸痱雉镢镬轭篝犷沐锰劣骘蝽麸忮弼犰踽翦麸泔眇豸翳屮疱泗邃沆狍粕盘沫呐粕紊陨衔扉篝镦骈屐溴骈铋糸镱彘翳弪溴骈铋糸镱礤翎镡赍泗矧溴骈铋糸镱溴沆狎狒轱铙咆砸镰粕盘沫刑捎鲠蜷徕戾怙躅麸痨轶轭麒殂躅腩秣骈屐潴狎麸忮汜汨邃鏖翳珏铙眢鲠祯屮疱泗邃沆狍蝈徜沆狍蝈徜豉疱啜戾舄è屮疱泗邃沆狍沆狍蟓骘蝽ì蝈徜沆狍篝蝈犴蝈徜篝蝓泗忮玳痱雉┅ì蝈徜豉疱篝蝓泗钺礤蝈徜沆狍螬┅躅戾篌ㄥ聃犰蝈徜豉疱篝蝓泗钺礤屮疱泗邃沆狍螬ㄩ铞犰殇篝蝓泗豉疱痱雉篝蝓泗钺礤屮疱泗邃沆狍螬蝈徜豉疱┅祜镳眭祠轲戾鲠祯瀛忾钿钺礤殇蝈徜骈屐洵豉疱篝蝈犴蝈徜骈屐洵忮玳痱雉麒孱ㄥ蝈徜骈屐洵豉疱篝镳蝈趱蝾┅ㄣ狍殇括祜镳骘驿轭骈屐洵溴骈铋糸镱骘殇ㄦ殄熹溴骈铋糸镱殇孱糸骈弪铛礅弪驿骘骈屐洵豉疱ㄦ殄熹溴骈铋糸镱豉疱驿滹扉篝驿殇泔祆邈啜殇ㄩㄥ聃犰蝈徜骈屐洵豉疱К豉疱汜翦顼蝙骈屐洵豉疱┅箦翩ㄦ殄熹溴骈铋糸镱钺礤驿篝蝈犴蝈徜鲠祯瀛狍痱雉К骈屐洵豉疱┅戾è鲠祯篝蝈犴蝈徜鲠祯瀛狍痱雉蝈徜骈屐洵豉疱┅ㄩ铞犰殇骈屐洵豉疱痱雉蝈徜沆狍殇钺礤К骈屐洵豉疱鲠祯濠换殒轸蝈趱蝾箦翩ㄦ殄熹溴骈铋糸镱钺礤驿鲠祯濠┅┅换栳钿戾躅腩秣骈屐潴戾舄è鲠祯篝蝈犴蝈徜鲠祯瀛狍痱雉蝈徜骈屐洵豉疱┅ㄦ躅腩秣瞽骈屐蝈徜沆狍钺礤殇蝈徜骈屐洵豉疱鲠祯濠┅ㄩ驿箦翩ㄧ弭屮趄岘骈屐洵痨轶ㄦ殄熹溴骈铋糸镱轭轸狎驿┅鲠祯濠躅腩秣瞽骈屐痱雉钺礤殇蝈徜骈屐洵豉疱鲠祯濠┅┅篝蝈犴蝈徜骈屐洵孱痱雉┅┅┅ㄤ彐磲泸溴姝蝈聃弩舡礤翳镤钺礤疳蜥礤翦颦扉篝蝈趱蝾豉疱蝈篝镳糸镱螬⑶孱弪狒蝈聃弩骢钽糸镱溴骈铋糸镱刘珥孱翳忉箦骢钽糸镱箝珙狒躜鏖翳犷轭轸獒疳蜥礤翦骘翳泔铑邈翦痱雉镢镬轭篝犷沐阵翳狒麸磲钺珏翳礤篌徵泔铙趄蹉糸镱翳蝈聃弩舣蝈痨痱镢弩蟋犷翳蝈篚祠溴泔溟铉义趱蝾翳蝈篚祠鲠祯矧箝珙犰犷屮沐痿轱狍疱翳蝈箴镱箦戾舄è殇孱糸骈弪矧箦泔钿ㄡ篌镢洪溴铘殒殄镳糸镱螬篝蜷铉钺礤┅ㄤ镢蹴孱翎糸镱箦泔钿ㄡ篌镢轰镢蹴孱翎糸镱镳糸镱螬┅ㄥ沐痿轱铙蝈篝ㄡ篌镢哄沐痿轱铙镳糸镱螬┅ㄥ沐痿轱瞽钺礤磲疸狎＇篝颦簌磲疸狎＇汜屮沐痿轱铙┅镱鬻狴箦泔钿ㄡ篌镢猴铄麽镳糸镱螬┅疳蜥礤翦颦钺礤磲疸狎＇灬礅溽ㄡ篝颦簌ㄦ轵篝岍┅疳蜥礤翦颦扉篝┅疳蜥礤翦颦殇磲疸狎＇翳轵疳蜥礤翦颦扉篝┅豉疱钺礤磲疸狎＇灬礅溽ㄡ豉疱钺礤沆狍箦泔钿岍┅疳蜥礤翦颦扉篝┅ㄣ犰飙篝蝓泗矧箦泔钿ㄡ篌镢恒犰飙篝蝓泗镳糸镱螬篝殇孱糸骈弪⑦狎珞┅蝈痨篝蝓泗矧箦泔钿ㄡ篌镢候屦禊篝蝓泗豉疱镳糸镱螬篝殇孱糸骈弪⑦蝈篚祠┅篚沣弩篝颦簌Ⅲ蹉沐篌┅鏖翳珏铙眢ㄧ痱雉屮趄岘轭轸狎珞啜痱镧ㄥ铙躜瀛珏铄蜷悱骢钽糸镱К钺礤红犴怃岘扉篝Ж痱雉镢镬鲤狎犴弭弪钺礤螬虹孱弪殂骢钽糸镱沆狍翳蜷骠蝈聃弩舡骢钽糸镱洪溴铘殒殄殇孱糸骈弪＋沣ㄣ沆汉蝈泔蜾狎珈轶К钺礤Ж痱雉镢镬鲤狎犴弭弪钺礤螬ㄤ彐礤翳镤钺礤è琊蝻痱雉镢镬括磲疸狎＇扉篝疳蜥礤翦颦钺礤豉疱钺礤螬括麒孱滹沲礤铘狒轱啜滹沲礤铘狒轱瞟篝蝈犴黩轸瀛礤篌徵瀛忮玳琊蝻殇孱糸骈弪с犰痱雉镢镬铄舡箦聃孱沐铛礅弪琊蝻舂换躞翳蝈箴邈糸鲥狎珞篝蝓泗躜狍翦眇灬翦麸珏铄蜥翦翳礤篌徵篝蝈犴黩轸瀛篝蝓泗琊蝻翳蜷骠红轶括磲疸狎＇灬礅溽ㄩ钺礤啜泔铙殇钺礤┅疳蜥礤翦颦殇疳蜥礤翦颦钺礤螬К篝颦簌汜祆篝蝓泗┅篝蝈犴黩轸瀛礤篌徵瀛孱琊蝻舂ㄩ镱鬻狴铋啜眭祠轲戾鲠祯瀛忾钿蝈聃弩舡礤篌徵瀛殇孱糸骈弪豉疱箦聃孱沐篝蝈犴蝈徜礤篌徵瀛忮玳琊蝻舂躅戾篌ㄥ耢箦聃孱沐痱雉镢镬箦聃孱沐铛礅弪琊蝻舂ㄩ铞犰殇箦聃孱沐铛礅弪琊蝻箦聃孱沐痱雉镢镬箦聃孱沐铛礅弪琊蝻舂┅躅戾篌ㄥ聃犰殇孱糸骈弪蝈聃弩舡礤篌徵瀛殇孱糸骈弪麽蝾Ⅱ弩痫铙滹弩铒磲翥蝈聃弩艉蟋螽殇孱糸骈弪蝈聃弩舡礤篌徵瀛殇孱糸骈弪┅ㄥ汜箦豉疱蝈痨戾ì括躅戾篌ㄥ蝈趱蝾豉疱鲲殇啜ì篚沣弩铋飑┅括祜镳骘钺礤轭屮沐痿轱瞽钺礤泔祆邈啜钺礤铋飑ì屮趄岘轭轸狎珞铋飑ㄧ孱弪狒瀛篝蝓泗溴泔溴琊蝻啜骈钿翳蜷骠沆狍К篝颦簌蝈痨篝蝓泗┅啜括躅戾篌ㄥ蝈趱蝾豉疱鲲殇啜ì篚沣弩铋洪呼疱蝈趱蝾豉疱┅厘沐痿轱铙屮趄岘轭轸狎珞篝蝈犴蝈徜礤篌徵瀛孱琊蝻舂括麒孱屮沐痿轱铙啜ㄣ镱括磲疸狎＇灬礅溽ㄥ啜屮蝈箴镱箦屮沐痿轱琊蝻蝈聃弩舡礤篌徵瀛殇孱糸骈弪箦聃孱沐屮┅屮沐痿轱瞽钺礤螬┅ㄩㄥ蝈趱蝾豉疱鲲殇铋篚沣弩┅è汜祆镱鬻狴换蝈沐轹邃汜祆镱鬻狴麒孱屮疱泗轭蝈箴镱箦躅屮疱泗邃蝈聃弩琊蝻蝈聃弩舡礤篌徵瀛殇孱糸骈弪箦聃孱沐痱镧篝蝈犴蝈徜篝蝓泗琊蝻舂篝蝈犴蝈徜礤篌徵瀛孱琊蝻舂┅ㄥ沐痿轱换蝈沐轹邃犷屮沐痿轱狍蝈箴镱箦蝈箴镱箦屮沐痿轱琊蝻蝈聃弩舡礤篌徵瀛殇孱糸骈弪箦聃孱沐痱镧篝蝈犴蝈徜篝蝓泗琊蝻舂篝蝈犴蝈徜礤篌徵瀛孱琊蝻舂┅┅┅┅┅ㄤ彐磲泸溴姝蝈箴镱箦礤翳镤钺礤疳蜥礤翦颦扉篝蝈趱蝾豉疱蝈篝镳糸镱螬⑶孱弪狒蝈箴镱箦骢钽糸镱溴骈铋糸镱澡礤翳镤轶溴骈铄鏖翳翳蝈狎珲礤铘蟋箦蝣殂瀣箦聃孱沐铛礅弪犷痱雉镢镬澡溴驷蹯礤翳镤溴泔溴翳溴沆狎邃狎珲礤铘篝蝓泗轭鲲脲翳忉箦镳弪狒矧犷洮溴疱钿轭镱翳蝈趱蝾豉疱孱泔溴蝈箴镱箦礤篌徵瀹澡玳鲥箦聃孱沐铛礅弪轶蝈躞邃轭翳蝈箴镱箦澡箦蝣殂狎珲礤铘轶狯衢灬忪骘箴邈獒扉狒轱瞵怩雉桢蝼轶殓铒蝈洚鏖翳珏铙眢箦蝣殂箦琊蝻屮趄岘狎珞戾舄è殇孱糸骈弪矧箦泔钿ㄡ篌镢洪溴铘殒殄镳糸镱螬篝蜷铉钺礤┅ㄤ镢蹴孱翎糸镱箦泔钿ㄡ篌镢轰镢蹴孱翎糸镱镳糸镱螬┅镱鬻狴箦泔钿ㄡ篌镢猴铄麽镳糸镱螬┅ㄩ眇戾礤铘狒轱矧箦泔钿ㄡ篌镢洪眇戾礤铘狒轱瞽骢钽糸镱镳糸镱螬ㄥ蝌矧⒘轫痨屙孱翎糸镱骢钽糸镱轶蝈聃轵邃┅疳蜥礤翦颦钺礤磲疸狎＇灬礅溽ㄡ篝颦簌ㄦ轵篝岍┅疳蜥礤翦颦扉篝┅ㄤ彐狨祠磲疸狎＇灬礅溽ㄡㄦ秕螋岍疳蜥礤翦颦扉篝┅ㄣ犰飙篝蝓泗矧箦泔钿ㄡ篌镢恒犰飙篝蝓泗镳糸镱螬篝殇孱糸骈弪⑦狎珞┅蝈痨篝蝓泗矧箦泔钿ㄡ篌镢候屦禊篝蝓泗镳糸镱螬篝殇孱糸骈弪⑦蝈篚祠┅ㄥ沐痿轱铙蝈篝ㄡ篌镢哄沐痿轱铙镳糸镱螬┅ㄡ痧扉汜糸镱骘蝽啜殒屮趄岘狎珞ㄡ痧禊＇轫痨屙孱翎糸镱鲤狎犴弭弪钺礤屮趄岘狎珞ì轫痨屙孱翎糸镱鲤狎犴弭弪钺礤螬┅ㄩㄦ怙躅漯轫痨屙孱翎糸镱啜痱镧ㄥ铙躜瀛珏铄蜷悱骢钽糸镱К钺礤红犴怃岘扉篝Ж箦蝣殂箦聃孱沐铛礅弪痱雉镢镬虹孱弪殂骢钽糸镱沆狍翳蜷骠蝈箴镱箦骢钽糸镱洪溴铘殒殄殇孱糸骈弪洪眇戾礤铘狒轱瞽骢钽糸镱ㄥ豉疱汜箦轫痨屙孱翎糸镱换溴驽翳弼犰踽糸镱簌礅镬啜聃雉轫痨屙孱翎糸镱┅è泔铙ㄥ耢灬礅溽┅啜骢钽糸镱轫痨屙孱翎糸镱┅┅＋沣ㄣ沆汉蝈泔蜾狎珈轶К钺礤Ж箦蝣殂箦聃孱沐铛礅弪痱雉镢镬┅ㄤ彐礤翳镤钺礤è箦蝣殂舂ì箦舂ì琊蝻痱雉镢镬┅括麒孱滹沲礤铘狒轱啜滹沲礤铘狒轱瞟戾ì括磲疸狎＇扉篝疳蜥礤翦颦钺礤溴驷蹯趔ì屮趄岘狎珞铋飑ㄧ孱弪狒瀛篝蝓泗溴泔溴琊蝻啜骈钿翳蜷骠沆狍К篝颦簌汜祆篝蝓泗┅磲疸狎＇疳蝽麸骈屐洵溴沆疳蜥礤翦颦扉篝屮趄岘狎珞戾è屮痱弩箝镱ㄣ镱镱鬻狴狃痨殂狒轱瞽骘蝽è羼蝈趱蝾豉疱鲲殇啜痱镧狃痨殂狒轱瞽骘蝽篝蝈犴黩轸瀛礤篌徵瀛忮玳琊蝻殇孱糸骈弪蝈痨箦瘵篝蝈犴黩轸瀛篝蝓泗琊蝻翳蜷骠红轶舂К篝颦簌蝈痨篝蝓泗┅篝蝈犴黩轸瀛礤篌徵瀛孱琊蝻舂┅啜戾è蝈篚祠狃痨殂狒轱瞽骘蝽┅篝蝈犴黩轸瀛礤篌徵瀛忮玳琊蝻殇孱糸骈弪蝈痨箦瘵篝蝈犴黩轸瀛篝蝓泗琊蝻翳蜷骠红轶ㄣ镱蝈篚祠┅К篝颦簌蝈痨篝蝓泗┅篝蝈犴黩轸瀛礤篌徵瀛孱琊蝻舂蝈篚祠┅┅ㄩ屮沐痿轱铙啜栳钿戾颦汜箦屮痱弩箝镱括祜镳骘屮沐痿轱瞽箴邈轭屮沐痿轱铙泔祆邈ㄤ弩趄蹉趱蜷铉忾钿ㄦ殄熹钺礤溴驷蹯脲豉疱殇屮沐痿轱瞽箴邈ㄤ邈灬蝈ㄩ珙矧骈屐洵钺礤溴驷蹯舂戾è屮翦蝾犰屮沐痿轱瞽豉疱箦泔钿豉疱┅啜篝颦簌屮翦蝾犰屮沐痿轱瞽豉疱ㄣ镱溟糸镱换箦铘狍蝈痨轭矧溴麸彐驽泗镳弪狒轱瞽箴邈殒殂屮沐痿轱换痱镢弩箝铉篝蝈犴黩轸瀛礤篌徵瀛忮玳琊蝻殇孱糸骈弪蝈痨箦瘵篝蝈犴黩轸瀛篝蝓泗琊蝻翳蜷骠红轶ㄣ镱殇泔钿轸轱瞟К篝颦簌蝈痨篝蝓泗┅篝蝈犴黩轸瀛礤篌徵瀛孱琊蝻舂泔钿轸轱瞟┅┅屮痱弩箝镱┅┅换殒铒轫痨屙孱翎糸镱轶痱弩孱衄麽蝾犷屙轸铒轭翦蜴徙痱镧麒孱泔眇殪瀛鲥蜮矬濯麽蝾⑽蝈箴镱箦轫痨屙孱翎糸镱痱弩孱艉螽轫痨屙孱翎糸镱┅鲠祯弩┅┅┅ㄤ彐磲泸溴姝箦蝣殂ㄩ溴铘殒殄忉箦箦蝣殂弩蝈篝镳糸镱螬⑶轹孱翳屮翦蝾犰钺礤骘翳箦蝣殂瀣犷镳糸镱犰轭桢蜷翎钽扉篝箪雉溴骈铋糸镱犷扉篝镦礤翳镤溴沆狎狒轱铙泔铙趄蹉沆狍溴骈铋糸镱麒殂轭沆蹁翳痱邈邃孱沐犷翳箪雉蟋犷痱秭殇弩礤翳镤忾钿轭珞骘翳蝈箴镱箦礤翳镤狍犷轭轸獒扉狒轱狎珲礤铘骑遽汨礤翳镤珏铄蜥翦蝈聃弩舣蝈痫铙礤翳镤疳轵温匀轶眭篝镳弪狒狍麸瓠戾鲥骘蝽轭矧溴翳狒翳狎珲礤铘篝蝓泗躜溴骈铋糸镱忮狯衢灬忪麸泔眇殪翳蝈聃弩舣蝈箴镱箦骢钽糸镱螽戾舄è钺礤篝颦簌殇孱糸骈弪┅ㄣ灬篌殇孱糸骈弪箦泔钿ㄡ篌镢恒灬篌镳糸镱螬┅ㄣ灬篌ㄩ沆狍蟓殇孱糸骈弪篝颦簌沆狍蟓殇孱糸骈弪箦蝣殂濠礤翳镤蝈盹鲥喉弭栾镳糸镱呼弩舡铒＇羼弘妁＇骈蝮舂ㄤ镢蹴孱翎糸镱箦泔钿ㄡ篌镢轰镢蹴孱翎糸镱镳糸镱螬┅ㄩ溴铘殒殄蝮磲疸狎＇箦泔钿礤翳镤螬蝈箴镱箦钺礤磲疸狎＇泔铙蝈箴镱箦簌礅镬殇孱糸骈弪螬ㄩ铋翎蜱祜镳骘脲蝈篝轭镳糸镱躅戾篌礤礅弪脲Ж后弪鲩沐沆狍喉弭栾轰镢蹴孱翎糸镱┅泔祆邈脲犷泔祆邈扉篝я躏翦蝈篝┅┅啜痱镧括磲疸犷＇灬礅溽礤翳镤溴沆狎狒轱瞟ㄤ弩趄蹉趱蜷铉忾钿ㄩ溴铘殒殄疳蜥礤翦颦扉篝蝈趱蝾豉疱脲镱鬻狴铋飑ㄥ沐痿轱铙铋飑ㄩ眇戾礤铘狒轱瞽骢钽糸镱篝颦簌殇孱糸骈弪┅滹沲礤铘狒轱瞟蝈篝礤翳镤溴沆狎狒轱瞟戾舄è汜祆篝蝓泗殇孱糸骈弪篝殇孱糸骈弪⑦狎珞┅蝈痨篝蝓泗殇孱糸骈弪篝殇孱糸骈弪⑦蝈篚祠┅蝈聃弩舡骢钽糸镱钺礤ㄣ镱蟓蝈聃弩舡簌礅镬殇孱糸骈弪┅蝈箴镱箦骢钽糸镱钺礤ㄣ镱蟓蝈箴镱箦簌礅镬殇孱糸骈弪┅啜ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄤ彐篝蝓泗汜祆篝蝓泗殇孱糸骈弪磲疸狎＇疳蝽麸骈屐洵溴沆疳蜥礤翦颦扉篝┅ㄤ彐篝蝓泗蝈痨篝蝓泗殇孱糸骈弪ì括躅戾篌ㄥ蝈趱蝾豉疱鲲殇啜á篚沣弩螈铋洪呼疱蝈趱蝾豉疱┅厘沐痿轱铙┅ㄥ痫螋К蝈聃弩舡骢钽糸镱钺礤簌礅镬疳汶徵К蝈聃弩舡骢钽糸镱钺礤┅ㄥ痫螋К蝈箴镱箦骢钽糸镱钺礤簌礅镬疳汶徵К蝈箴镱箦骢钽糸镱钺礤┅ㄤ彐蝈聃弩舡礤翳镤蝈聃弩舡骢钽糸镱钺礤ì疳蜥礤翦颦扉篝蝈趱蝾豉疱ê殇孱糸骈弪殇孱糸骈弪括麒孱滹沲礤铘狒轱啜ê滹沲礤铘狒轱篝蜷铉趄轫麒轸弩疳沐滹沲礤铘狒轱瞟┅ê汜祆篝蝓泗汜祆篝蝓泗殇孱糸骈弪ê蝈痨篝蝓泗蝈痨篝蝓泗殇孱糸骈弪括麒孱屮沐痿轱铙啜ê屮沐痿轱铙厘沐痿轱铙┅括麒孱镱鬻狴啜ê镱鬻狴舂┅ㄤ彐蝈箴镱箦礤翳镤蝈箴镱箦骢钽糸镱钺礤ì疳蜥礤翦颦扉篝蝈趱蝾豉疱ê殇孱糸骈弪殇孱糸骈弪ê汜祆篝蝓泗汜祆篝蝓泗殇孱糸骈弪ê蝈痨篝蝓泗蝈痨篝蝓泗殇孱糸骈弪ê轫痨屙孱翎糸镱骢钽糸镱轫痨屙孱翎糸镱骢钽糸镱括麒孱屮沐痿轱铙啜ê屮沐痿轱铙厘沐痿轱铙┅括麒孱镱鬻狴啜ê镱鬻狴舂┅┅┅礤翳镤螬换屮痫螋翳箦蝣殂钺礤镱禊ㄥ鲠飙麒孱ê泔眇殪瀛麸痨弼屐红镝洵麸痨弼屐哄邈豸濠ㄥ痫螋К钺礤簌礅镬疳汶徵К钺礤┅换泔铙趄蹉犷忾钿翳珈镡犰箦蝣殂轭篝犷沐ㄤ彐疳蜥礤翦钺礤磲脲轭篝犷沐К沆狍洪溴铘殒殄殇孱糸骈弪衡狍瀛箦蝣殂弩扉篝括磲疸狎＇篝颦簌ㄩ扉篝忉箦箦蝣殂弩忉箦箦蝣殂弩扉篝忉箦箦蝣殂弩┅┅喉弭栾潴К磲疸狎＇灬礅溽ㄩ溴铘殒殄钺礤啜殇孱糸骈弪钺礤┅殇孱糸骈弪蝈箴镱箦钺礤螬括麒孱滹沲礤铘狒轱啜轰镢蹴孱翎糸镱篝蜷铉趄轫麒轸弩疳沐滹沲礤铘狒轱瞟┅篱铋翎蜱螬┅┅